definition {
    @summary = "Fill the goals page form"
    macro fillGoalsPage {
        Select(
            locator1 = "PRMMDFRequest#SELECT",
            key_fieldName = "company",
            value1 = "${accountName}");

        Select(
            locator1 = "PRMMDFRequest#SELECT",
            key_fieldName = "country",
            value1 = "${countryName}");

        Type(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "overallCampaignName",
            value1 = "${campaignName}");

        Type(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "overallCampaignDescription",
            value1 = "${campaignDescription}");

        for (var goalsList : list "${goalsOptions}") {
            Click.clickAtNotVisible(
                locator1 = "PRMMDFRequest#CHECKBOX",
                key_fieldName = "liferayBusinessSalesGoals",
                key_optionValue = "${goalsList}");
        }

        for (var targetMarketList : list "${targetMarketOptions}") {
            Click.clickAtNotVisible(
                locator1 = "PRMMDFRequest#CHECKBOX",
                key_fieldName = "targetMarkets",
                key_optionValue = "${targetMarketList}");
        }

        if (isSet(additionalOptions)) {
            for (var optionsList : list "${additionalOptions}") {
                Click.clickAtNotVisible(
                    locator1 = "PRMMDFRequest#INPUT_BUTTON",
                    key_fieldName = "additionalOption",
                    key_buttonText = "${optionsList}");
            }
        }

        for (var targetRolesList : list "${targetRolesOptions}") {
            Click.clickAtNotVisible(
                locator1 = "PRMMDFRequest#CHECKBOX",
                key_fieldName = "targetAudienceRoles",
                key_optionValue = "${targetRolesList}");
        }
    }

    @summary = "Add a new activity"
    macro newActivity {
        if (!(isSet(activityIndex))) {
            var activityIndex = "0";
        }

        Click(
            locator1 = "PRMMDFRequest#ADD_BUTTON",
            key_buttonText = "Add Activity");

        Type(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "activities[${activityIndex}].name",
            value1 = "${activityName}");

        Select(
            locator1 = "PRMMDFRequest#SELECT",
            key_fieldName = "activities[${activityIndex}].typeActivity",
            value1 = "${typeOfActivity}");

        Select(
            locator1 = "PRMMDFRequest#SELECT",
            key_fieldName = "activities[${activityIndex}].tactic",
            value1 = "${tactic}");

        var currentDay = DateUtil.getCurrentDate();
		var currentMonth = DateUtil.getCurrentMonth();
		var currentYear = DateUtil.getCurrentYear();
		var nextDayStartDate = DateUtil.getFormattedDate("+1", "dd");

		if ("${nextDayStartDate}" == "01") {
			var currentMonth = ${currentMonth} + 1;

            if ("${currentMonth}" == "13") {
                fail("The selected date will be on next year. The start date can be only on current year");
            }
		}

		var currentDate = "${currentMonth}${nextDayStartDate}${currentYear}";

        Type.sendKeys(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "activities[${activityIndex}].startDate",
            value1 = "${currentDate}");
        
        var nextDayEndDate = DateUtil.getFormattedDate("+2", "dd");

	    var endIsLessThanStart = MathUtil.isLessThan("${nextDayEndDate}", "${nextDayStartDate}");

		if ("${endIsLessThanStart}" == "true") {
			var currentMonth = ${currentMonth} + 1;

             if ("${currentMonth}" == "13") {
                fail("The end date can not be different from start date.");
            }
		}

        var futureDate = "${currentMonth}${nextDayEndDate}${currentYear}";

        Type.sendKeys(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "activities[${activityIndex}].endDate",
            value1 = "${futureDate}");

        var budgetIndex = "0";

        for (var bugdetValues : list "${budgetList}") {
            var expenseOption = StringUtil.extractFirst("${bugdetValues}", "-");
            var bugdetValue = StringUtil.extractLast("${bugdetValues}", "-");

            Click(
                locator1 = "PRMMDFRequest#ADD_BUTTON",
                key_buttonText = "Add Expense");

            Select(
                locator1 = "PRMMDFRequest#SELECT",
                key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].expense",
                value1 = "${expenseOption}");

            Type(
                locator1 = "PRMMDFRequest#TEXT_FIELD",
                key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].cost",
                value1 = "${bugdetValue}");

            var budgetIndex = ${budgetIndex} + 1;
        }

        Click.clickAtNotVisible(
            locator1 = "PRMMDFRequest#CHECKBOX",
            key_fieldName = "activities[${activityIndex}].leadGenerated",
            key_optionValue = "${leadList}");

        Type(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "activities[${activityIndex}].targetOfLeads",
            value1 = "${targetOfLeads}");

        for (var leadFollowUp : list "${leadFollowUpList}") {
            Click.clickAtNotVisible(
                locator1 = "PRMMDFRequest#CHECKBOX",
                key_fieldName = "activities[${activityIndex}].leadFollowUpStrategies",
                key_optionValue = "${leadFollowUp}");
        }

        Type(
            locator1 = "PRMMDFRequest#TEXT_FIELD",
            key_fieldName = "activities[${activityIndex}].detailsLeadFollowUp",
            value1 = "${followUpStrategy}");
    }

    @summary = "Add a new MDF Request filling the goals and activity pages"
    macro createNewRequest {
        PRMMDFRequest.fillGoalsPage(
            accountName = "${accountName}",
            countryName = "${countryName}",
            campaignName = "${campaignName}",
            campaignDescription = "${campaignDescription}",
            targetMarketOptions = "${targetMarketOptions}",
            goalsOptions = "${goalsOptions}",
            targetRolesOptions = "${targetRolesOptions}",
            additionalOptions = "${additionalOptions}");

        Button.click(button = "Continue");

        Click(
            locator1 = "PRMMDFRequest#ADD_BUTTON",
            key_buttonText = "Add Activity");

        PRMMDFRequest.newActivity(
            activityName = "${activityName}",
            typeOfActivity = "${typeOfActivity}",
            tactic = "${tactic}",
            budgetList = "${budgetList}",
            leadList = "${leadList}",
            targetOfLeads = "${targetOfLeads}",
            leadFollowUpList = "${leadFollowUpList}",
            followUpStrategy = "${followUpStrategy}");

        Button.click(button = "Continue");

        Button.click(button = "Continue");

        ScrollWebElementIntoView(locator1 = "Button#SUBMIT");

        Button.click(button = "Submit");

        WaitForPageLoad();
    }
}