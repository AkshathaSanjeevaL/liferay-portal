definition {

	macro addEVPSite {
		Site.openSitesAdmin();

		TeamExtranet.addSiteTemplateCP(
			siteName = "EVP Portal",
			siteTemplateName = "EVP");
	}

	macro assertCardTitle {
		for (var cardTitle : list ${listcardTitle}) {
			AssertElementPresent(
				key_card = ${cardTitle},
				locator1 = "Card#CARD_TITLE");
		}
	}

	macro downloadFileViaCurl {
		var currentURL = selenium.getCurrentUrl();
		var testDependenciesDir = PropsUtil.get("output.dir.name");

		var curl = '''${currentURL} --create-dirs -o ${testDependenciesDir}/${fileName}''';

		echo("curl = ${curl}");

		com.liferay.poshi.runner.util.JSONCurlUtil.get(${curl});
	}

	@summary = "Fill the date fields"
	macro insertDate {
		var currentDate = DateUtil.getFormattedDate(${dateRange}, "yyyy-MM-dd");

		var nextDayDate = DateUtil.getFormattedDate("+2", "yyyy-MM-dd");
		
		Type.sendKeys(
			key_fieldLabel = ${initialDateLabel},
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = ${currentDate});

		Type.sendKeys(
			key_fieldLabel = ${endDateLabel},
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = ${nextDayDate});
	}

	@summary = "generate Financial Report for EVP"
	macro generateFinancialReport {
		LexiconCard.clickCardTitle(card = ${cardTitle});

		EVPUtils.insertDate(
            dateRange = ${dateRange},
            endDateLabel = ${endDateLabel},
            initialDateLabel = ${initialDateLabel});

		Button.click(button = "Generate");
	}

	@summary = "Navigating to EVP needs an extra click on Sign In which will redirect to Okta"
	macro gotoEVP {
		Navigator.openSiteURL(siteName = "EVP Portal");

		if (IsElementPresent(locator1 = "EVPUtils#SIGN_IN")) {
			Click(locator1 = "EVPUtils#SIGN_IN");
		}
	}

}