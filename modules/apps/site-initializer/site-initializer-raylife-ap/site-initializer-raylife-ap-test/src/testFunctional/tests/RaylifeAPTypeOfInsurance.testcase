@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-165493=true";
	property osgi.modules.includes = "site-initializer-raylife-ap";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Raylife AP";

	var testSiteName = "Raylife AP";
	var testSiteURL = "raylife-ap";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Raylife AP Site") {
			Site.openSitesAdmin();

			Raylife.createRaylifeSite(siteName = "Raylife D2C");

			Raylife.createRaylifeSite(siteName = "Raylife AP");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		RaylifeAP.tearDownCP();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "Verify if the user can swicth between the cards."
	@priority = 3
	test IfCanSwitchBetweenCards {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfCanSwitchBetweenCards";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the modal for new application is open") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			Button.click(button = "Application");
		}

		task ("Then Assert if the user can switch between the cards") {
			RaylifeApApplication.modalOption(option = "Business");

			RaylifeApApplication.assertModalInfos(option = "Business");

			RaylifeApApplication.modalOption(option = "Personal");

			RaylifeApApplication.assertModalInfos(option = "Personal");

			Button.click(button = "Next");

			RaylifeApApplication.assertModalInfos(option = "Auto");
		}
	}

	@description = "Verify if when the user selects a card, go to next page and click on 'Previous' button the card still selected (active)."
	@priority = 4
	test IfCardStillSelectedWhenUserGoToPreviousPage {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfCardStillSelectedWhenUserGoToPreviousPage";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the New Application modal is open") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			RaylifeApApplication.modalFlow(
				button = "Previous",
				insurance = "Personal");
		}

		task ("Then Assert that the Insurance options is still active after click in 'previous' button") {
			RaylifeApApplication.assertModalInfos(
				notOption = "Busines",
				option = "Personal");
		}
	}

	@description = "Verify if the 'New Applications' button from the side-bar option 'Applications', works."
	@priority = 5
	test IfNewApplicationsFromSideBarWorks {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfNewApplicationsFromSideBarWorks";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the modal new application is open by side-bar option") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

			Button.click(button = "New Applications");
		}

		task ("Then Assert that the modal is open") {
			RaylifeApApplication.assertModalInfos(description = "What type of insurance does this customer need?");
		}
	}

}