@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-165493=true";
	property osgi.modules.includes = "site-initializer-raylife-ap";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Raylife AP";

	var testSiteName = "Raylife AP";
	var testSiteURL = "raylife-ap";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Raylife AP Site") {
			Site.openSitesAdmin();

			Raylife.createRaylifeSite(siteName = "Raylife D2C");

			Raylife.createRaylifeSite(siteName = "Raylife AP");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		RaylifeAP.tearDownCP();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "Verify if the user can swicth between the cards."
	@priority = 3
	test IfCanSwitchBetweenCards {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfCanSwitchBetweenCards";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the modal for new application is open") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			Click(
				key_text = "Application",
				locator1 = "Button#ANY");
		}

		task ("Then Assert if the user can switch between the cards") {
			RaylifeApApplication.modalOption(option = "Business");

			AssertElementPresent(
				key_text = "Business",
				locator1 = "RaylifeApplicationModal#MODAL_INSURANCE");

			RaylifeApApplication.modalOption(option = "Personal");

			AssertElementPresent(
				key_text = "Personal",
				locator1 = "RaylifeApplicationModal#MODAL_INSURANCE");

			Click(
				key_text = "Next",
				locator1 = "Button#ANY");

			RaylifeApApplication.modalOption(option = "Auto");

			AssertElementPresent(
				key_text = "Auto",
				locator1 = "RaylifeApplicationModal#MODAL_INSURANCE");
		}
	}

	@description = "Verify if when the user selects a card, go to next page and click on 'Previous' button the card still selected (active)."
	@priority = 4
	test IfCardStillSelectedWhenUserGoToPreviousPage {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfCardStillSelectedWhenUserGoToPreviousPage";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the New Application modal is open") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			RaylifeApApplication.modalFlow(
				button = "Previous",
				insurance = "Personal");
		}

		task ("Then Assert that the Insurance options is still active after click in 'previous' button") {
			AssertElementNotPresent(
				key_text = "Business",
				locator1 = "RaylifeApplicationModal#MODAL_INSURANCE");

			AssertElementPresent(
				key_text = "Personal",
				locator1 = "RaylifeApplicationModal#MODAL_INSURANCE");
		}
	}

	@description = "Verify if the 'New Applications' button from the side-bar option 'Applications', works."
	@priority = 5
	test IfNewApplicationsFromSideBarWorks {
		property test.name.skip.portal.instance = "RaylifeAPTypeOfInsurance#IfNewApplicationsFromSideBarWorks";

		// Skip portal instance due to LRQA-79129. Unskip after it is resolved

		task ("Given the modal new application is open by side-bar option") {
			RaylifeAP.goToRaylifeAPSiteWithRefresh();

			RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

			Click(
				key_text = "New Applications",
				locator1 = "Button#ANY");
		}

		task ("Then Assert that the modal is open") {
			AssertElementPresent(
				key_text = "What type of insurance does this customer need?",
				locator1 = "RaylifeApplicationModal#MODAL_DESCRIPTION");
		}
	}

}