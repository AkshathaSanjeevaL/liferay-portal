definition {

	@summary = "This macro add a template via UI."
	macro addTemplate {
		Click(locator1 = "Button#PLUS");

		Click.javaScriptClick(
			key_picklistOption = ${dropdownItem},
			locator1 = "TestrayDropdown#PICKLIST_KEBAB_MENU_OPTION");

		if (isSet(fieldName)) {
			Type(
				inputFieldId = "name",
				locator1 = "TextInput#INPUT_ID",
				value1 = ${fieldName});
		}

		if (isSet(routineName)) {
			Select(
				locator1 = "TestrayBuild#ROUTINE_ID",
				value1 = ${routineName});
		}

		if (isSet(gitHash)) {
			Type(
				inputFieldId = "gitHash",
				locator1 = "TextInput#INPUT_ID",
				value1 = ${gitHash});
		}

		if (isSet(description)) {
			Type(
				locator1 = "TextArea#DESCRIPTION",
				value1 = ${description});
		}

		Button.clickSave();
	}

	@summary = "This macro assert a template name in the search bar."
	macro assertTemplateName {
		Click(locator1 = "Button#PLUS");

		AssertElementPresent(
			locator1 = "TestrayTemplate#SEARCH_TEMPLATE",
			templateName = ${templateName});
	}

	@summary = "This macro go to template."
	macro goToBuildTemplate {
		Click(locator1 = "Button#PLUS");

		Click.javaScriptClick(
			key_templateName = ${templateName},
			locator1 = "TestrayDropdown#TEMPLATE_DROPDOWN");
	}

	@summary = "This macro assert template details in the table."
	macro viewTemplateDetails {
		AssertElementPresent(
			locator1 = "TestrayTemplate#TEMPLATE_DETAILS",
			templateName = ${templateName},
			templateStatus = ${templateStatus});
	}

}