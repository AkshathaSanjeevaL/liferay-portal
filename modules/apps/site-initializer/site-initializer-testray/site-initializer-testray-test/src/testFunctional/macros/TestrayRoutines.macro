definition {

	@summary = "This macro assert that the status of the build is disabled."
	macro assertStatusBuildDisabled {
		AssertElementPresent(
			key_item = ${key_item},
			locator1 = "TestrayRoutines#STATUS_BUILD_HISTORY_DISABLED");
	}

	@summary = "This macro click in the status of the build."
	macro clickStatusBuildHistory {
		Click(
			key_item = ${key_item},
			locator1 = "TestrayRoutines#STATUS_BUILD_HISTORY");
	}

	@summary = "This macro add a routine via UI."
	macro createRoutine {
		Click(locator1 = "Testray#ADD_PROJECT");

		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = ${key_name});

		Click(locator1 = "TestrayManage#SAVE_BUTTON");
	}

	@summary = "This macro delete a routine."
	macro deleteRoutine {
		Testray.deleteComponent(itemName = ${itemName});
	}

	@summary = "This macro delete a routine in the setup."
	macro deleteRoutineSetup {
		TestrayLayout.clickResultsOnSidebar();

		Refresh();

		Testray.changePagination(
			numberItems = 20,
			valuePagination = 150);

		if (IsElementPresent(key_projectName = "Projects_", locator1 = "Testray#ITEM_NAME")) {
			Testray.goToProjectPage(projectName = "Projects_1 Project");
		}

		if (IsElementPresent(key_projectName = "Routines_1 Routine", locator1 = "Testray#ITEM_NAME")) {
			TestrayRoutines.deleteRoutine(itemName = "Routines_1 Routine");

			Testray.checkSuccessAlertAppearsAndDisappears();
		}
	}

	@summary = "This macro edit a routine."
	macro editRoutine {
		TestrayManage.editProductVersion(
			caseName = ${newNameRoutine},
			itemManage = "Edit",
			valueName = ${routineName});
	}

	@summary = "This macro go to routine."
	macro goToRoutine {
		Click(
			key_projectName = ${routineName},
			locator1 = "Testray#ITEM_NAME");
	}

	@summary = "This macro add environment factors via UI."
	macro selectDefaultEnvironmentFactors {
		Testray.kebabMenuOption(kebabOption = "Select Default Environment Factors");

		WaitForElementPresent(
			key_environmentFactors = "Application Server",
			locator1 = "Testray#SELECT_ENVIRONMENT_FACTORS");

		for (var listboxItem : list ${listCategory}) {
			Click(
				key_environmentFactors = ${listboxItem},
				locator1 = "Testray#SELECT_ENVIRONMENT_FACTORS");

			Click(locator1 = "Testray#AUDIT_GRAPH_CONFIGURATION_RIGHT_ARROW_BUTTON");
		}

		Button.click(button = "Next");

		Select(
			key_fieldLabel = ${categoryField},
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = ${listOption});

		if (isSet(categoryField2)) {
			Select(
				key_fieldLabel = ${categoryField2},
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${listOption2});
		}

		Button.click(button = "Save");
	}

	@summary = "This macro select option in the dropdown on routine."
	macro selectOptionDropdownRoutine {
		Click(locator1 = "TestrayRoutines#DROPDOWN_ROUTINE");

		MenuItem.clickNoError(menuItem = ${menuItem});

		Alert.viewSuccessMessage();
	}

	@summary = "This macro writes into the routine field."
	macro typeRoutineField {
		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = ${key_name});

		Click(locator1 = "TestrayManage#SAVE_BUTTON");
	}

}