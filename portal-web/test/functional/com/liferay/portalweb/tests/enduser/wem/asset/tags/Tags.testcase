@component-name = "portal-tags"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Tags";
	property testray.main.component.name = "Tags";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			HeadlessSite.addSite(siteName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "LPS-169877 10 instances of Tags fragment can be added into the same form container"
	@priority = 4
	test CanAdd10TagsFragmentsToPage {
		task ("Given an object with scope to a site") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Global",
					tagName = "global tag ${i}");
			}

			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "local tag");

			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject",
				scope = "site");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout Name",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Site Administration > Categorization");

			Button.clickSave();

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			ObjectAdmin.goToLayoutsTab();

			ObjectAdmin.selectKebabMenuOption(kebabOption = "View");

			ObjectAdmin.markLayoutAsDefault();

			ObjectAdmin.goToLayoutTabOnLayouts();

			ObjectAdmin.addTabFieldsOnLayout(
				blockName = "Block 1",
				columnsNumber = 1,
				fieldLabelName = "Name",
				tabName = "Field Tab");

			// Need to rewrite when LPS-182835 get fixed

			ObjectAdmin.addCategorizationOnLayoutTab();

			Button.clickSave();

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Form Components",
				fragmentName = "Form Container");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.viewInputStyle(
				label = "Name",
				showLabel = "true",
				type = "text-input");

			for (var i : list "1,2,3,4,5,6,7,8,9,10") {
				PageEditor.addFragmentToFormContainer(
					collectionName = "Form Components",
					fragmentName = "Tags",
					targetFragmentName = "Text Input");
			}

			PageEditor.publish();
		}

		task ("When we add tags as input") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1",
				tagRow = 1);

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 2",
				tagRow = 4);

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 2,local tag",
				tagRow = 6);

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2,local tag",
				tagRow = 7);

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntryDetails(entry = "Test Name");

			// Need to rewrite when LPS-182835 get fixed

			for (var tagName : list "global tag 1,global tag 2,local tag") {
				AssetCategorization.viewTag(tagName = ${tagName});
			}
		}
	}

	@description = "The user cannot add a tag with an empty name."
	@priority = 4
	@uitest
	test CannotAddTagWithoutName {
		task ("View cannot add a tag wih an empty name") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.addWithNullTitleCP(tagName = " ");
		}

		task ("View empty message on tags admin") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Portlet.viewEmptyMessage(message = "There are no tags.");
		}
	}

	@description = "View cannot merge single tag on tags admin."
	@priority = 4
	test CannotMergeSingleTarget {
		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname");
		}

		task ("View cannot merge single tag") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.gotoMerge(
				mergeMethod = "menuItem",
				tagName = "tagname");

			Tag.mergeSingleTagWarning();
		}
	}

	@description = "LPS-169877 The tag field can be used in a master page"
	@priority = 3
	test CanUseTagsInMasterPage {
		task ("Given a master page with the tag fragment") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Global",
					tagName = "global tag ${i}");
			}

			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "local tag");

			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject",
				scope = "site");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout Name",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Site Administration > Categorization");

			Button.clickSave();

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			ObjectAdmin.goToLayoutsTab();

			ObjectAdmin.selectKebabMenuOption(kebabOption = "View");

			ObjectAdmin.markLayoutAsDefault();

			ObjectAdmin.goToLayoutTabOnLayouts();

			ObjectAdmin.addTabFieldsOnLayout(
				blockName = "Block 1",
				columnsNumber = 1,
				fieldLabelName = "Name",
				tabName = "Field Tab");

			ObjectAdmin.addCategorizationOnLayoutTab();

			Button.clickSave();

			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Master Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");

			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Master Page Template");

			PageEditor.addFragment(
				collectionName = "Form Components",
				dropTarget = "Header",
				fragmentName = "Form Container",
				scope = "Master");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.viewInputStyle(
				label = "Name",
				showLabel = "true",
				type = "text-input");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Text Input");

			MastersPageTemplates.publishMaster();
		}

		task ("When we create a page with the created master page") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Master Page Template",
				pageName = "Test Page Name");

			PageEditor.publish();
		}

		task ("Then it is possible to send tags as entries") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2,local tag");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntryDetails(entry = "Test Name");

			for (var tagName : list "global tag 1,global tag 2,local tag") {
				AssetCategorization.viewTag(tagName = ${tagName});
			}
		}
	}

	@description = "LPS-169877 The tag field can be used in a page template"
	@priority = 5
	test CanUseTagsInPageTemplate {
		task ("Given a page template with the tag fragment") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Global",
					tagName = "global tag ${i}");
			}

			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "local tag");

			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject",
				scope = "site");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout Name",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Site Administration > Categorization");

			Button.clickSave();

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			ObjectAdmin.goToLayoutsTab();

			ObjectAdmin.selectKebabMenuOption(kebabOption = "View");

			ObjectAdmin.markLayoutAsDefault();

			ObjectAdmin.goToLayoutTabOnLayouts();

			ObjectAdmin.addTabFieldsOnLayout(
				blockName = "Block 1",
				columnsNumber = 1,
				fieldLabelName = "Name",
				tabName = "Field Tab");

			ObjectAdmin.addCategorizationOnLayoutTab();

			Button.clickSave();

			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection");

			JSONLayoutpagetemplate.addLayoutPageTemplateEntry(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection",
				layoutPageTemplateEntryName = "Page Template Name",
				layoutPageTemplateStatus = "Draft",
				layoutPageTemplateType = "Content Page Template");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.gotoPageTemplate(pageTemplateName = "Page Template Name");

			PageEditor.addFragment(
				collectionName = "Form Components",
				fragmentName = "Form Container");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.viewInputStyle(
				label = "Name",
				showLabel = "true",
				type = "text-input");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Text Input");

			PageEditor.publish();
		}

		task ("When we create a page with the created template") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Page Name",
				pageTemplateName = "Page Template Name");

			PageEditor.publish();
		}

		task ("Then it is possible to send tags as entries") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2,local tag");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntryDetails(entry = "Test Name");

			for (var tagName : list "global tag 1,global tag 2,local tag") {
				AssetCategorization.viewTag(tagName = ${tagName});
			}
		}
	}

	@description = "The user can delete multiple tags via Toolbar management."
	@priority = 4
	test DeleteMultipleTagsViaToolbar {
		task ("Add three tags") {
			for (var i : list "1,2,3") {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname${i}");
			}
		}

		task ("View tags are shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "Test Site Name");

			Tag.viewCP(tagNameList = "tagname1,tagname2,tagname3");
		}

		task ("Delete tags via Toolbar management") {
			Tag.deleteByIconCP(tagNameList = "tagname1,tagname2,tagname3");
		}

		task ("View tags are not shown") {
			Portlet.viewEmptyMessage(message = "There are no tags.");
		}
	}

	@description = "The user can delete single tag via Toolbar management."
	@priority = 4
	test DeleteSingleTagViaToolbar {
		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname");
		}

		task ("View tag is shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.viewCP(tagNameList = "tagname");
		}

		task ("Delete tag via Toolbar management") {
			Tag.deleteByIconCP(tagNameList = "tagname");
		}

		task ("View tag is not shown") {
			Portlet.viewEmptyMessage(message = "There are no tags.");
		}
	}

	@description = "The user can delete a single tag via menu item."
	@priority = 5
	test DeleteTagViaMenu {
		property portal.acceptance = "true";

		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname");
		}

		task ("View tag is shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.viewCP(tagNameList = "tagname");
		}

		task ("Delete tag via menu item") {
			Tag.deleteCP(tagNameList = "tagname");
		}

		task ("View tag is not shown") {
			Portlet.viewEmptyMessage(message = "There are no tags.");
		}
	}

	@description = "The user can edit the tag name."
	@priority = 5
	test EditTag {
		property portal.acceptance = "true";

		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname1");
		}

		task ("View tag is shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.viewCP(tagNameList = "tagname1");
		}

		task ("Edit tag name without save") {
			Tag.editCP(
				tagMenuName = "tagname1",
				tagNameEdit = "tagname2");
		}

		task ("View edited tag name is shown") {
			Tag.viewCP(tagNameList = "tagname2");
		}

		task ("View old tag name is not shown") {
			Tag.viewNotPresentCP(tagNameList = "tagname1");
		}
	}

	@description = "The edited tag name is not shown whether the change is not saved."
	@priority = 4
	test EditTagWithoutSave {
		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname1");
		}

		task ("View tag is shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			Tag.viewCP(tagNameList = "tagname1");
		}

		task ("Edit tag name without save") {
			Tag.cancelEditCP(
				tagMenuName = "tagname1",
				tagNameEdit = "tagname2");
		}

		task ("View original tag name is shown") {
			Tag.viewCP(tagNameList = "tagname1");
		}

		task ("View edited tag name is not shown") {
			Tag.viewNotPresentCP(tagNameList = "tagname2");
		}
	}

	@description = "The usar can select another tag to merge in Merge Tags section."
	@priority = 3
	test MergeAdditionalTag {
		task ("Add six tags") {
			for (var i : list "1,2,3,4,5,6") {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname${i}");
			}
		}

		task ("View six tags are shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "Test Site Name");

			Tag.viewCP(tagNameList = "tagname1,tagname2,tagname3,tagname4,tagname5,tagname6");
		}

		task ("Select first, third and fifth tag to merge") {
			Tag.gotoMerge(tagNameList = "tagname1,tagname3,tagname5");
		}

		task ("Select an additional tag to merge") {
			Tag.mergeTagsSelectAdditionalTags(tagNameList = "tagname6");
		}

		task ("Merge tags into fifth tag") {
			Tag.mergeTags(
				mergedTag = "tagname5",
				tagNameList = "tagname5,tagname3,tagname1,tagname6");
		}

		task ("View second, fourth and fifth tag are shown") {
			Tag.viewCP(tagNameList = "tagname2,tagname4,tagname5");
		}

		task ("View first, third and sixth tag are not shown") {
			Tag.viewNotPresentCP(tagNameList = "tagname1,tagname3,tagname6");
		}
	}

	@description = "The user can merge multiple tags."
	@priority = 4
	test MergeMultipleTags {
		task ("Add three tags") {
			for (var i : list "1,2,3") {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname${i}");
			}
		}

		task ("View three tags are shown in tags admin") {
			Tag.openTagsAdmin(siteURLKey = "Test Site Name");

			Tag.viewCP(tagNameList = "tagname1,tagname2,tagname3");
		}

		task ("Merge second and third tag into first tag") {
			Tag.gotoMerge(tagNameList = "tagname1,tagname2,tagname3");

			Tag.mergeTags(
				mergedTag = "tagname1",
				tagNameList = "tagname3,tagname2,tagname1");
		}

		task ("View first tag is shown") {
			Tag.viewCP(tagNameList = "tagname1");
		}

		task ("View second and third tag are not shown") {
			Tag.viewNotPresentCP(tagNameList = "tagname2,tagname3");
		}
	}

	@description = "View display styles in tags admin."
	@priority = 4
	test ToggleDisplayStyles {
		task ("Add a tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname");
		}

		task ("View tag is shwon in list style") {
			Tag.openTagsAdmin(siteURLKey = "Test Site Name");

			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			LexiconList.viewListEntryTitle(listEntry = "tagname");
		}

		task ("View tag is shown in table style") {
			LexiconEntry.changeDisplayStyle(displayStyle = "table");

			LexiconTable.viewTableEntryTitle(tableEntry = "tagname");
		}
	}

	@description = "LPS-169877 Both site and global tags are shown when the object scope is set to the site"
	@priority = 5
	test ViewAllTagsShownInObjectSiteScoped {
		task ("Given an object with scope to a site") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Global",
					tagName = "global tag ${i}");
			}

			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "local tag");

			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject",
				scope = "site");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout Name",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Site Administration > Categorization");

			Button.clickSave();

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			ObjectAdmin.goToLayoutsTab();

			ObjectAdmin.selectKebabMenuOption(kebabOption = "View");

			ObjectAdmin.markLayoutAsDefault();

			ObjectAdmin.goToLayoutTabOnLayouts();

			ObjectAdmin.addTabFieldsOnLayout(
				blockName = "Block 1",
				columnsNumber = 1,
				fieldLabelName = "Name",
				tabName = "Field Tab");

			ObjectAdmin.addCategorizationOnLayoutTab();

			Button.clickSave();

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Form Components",
				fragmentName = "Form Container");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Form Container");

			Alert.viewErrorMessage(errorMessage = "Fragments cannot be placed inside an unmapped form container.");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.viewInputStyle(
				label = "Name",
				showLabel = "true",
				type = "text-input");

			PageEditor.viewFragmentNotPresent(fragmentName = "Tags");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Text Input");

			PageEditor.publish();
		}

		task ("When we add tags as input") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2,local tag");
		}

		task ("Then both site and global tags are shown") {
			for (var i : list "1,2") {
				AssertTextEquals(
					key_tagName = "global tag ${i}",
					locator1 = "AssetCategorization#TAG",
					value1 = "global tag ${i}");
			}

			AssertTextEquals(
				key_tagName = "local tag",
				locator1 = "AssetCategorization#TAG",
				value1 = "local tag");

			AssetCategorization.removeTag(tagName = "local tag");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "local tag");

			for (var i : list "1,2") {
				AssetCategorization.removeTag(tagName = "global tag ${i}");
			}

			AssetCategorization.removeTag(tagName = "local tag");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2,local tag");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntryDetails(entry = "Test Name");

			for (var tagName : list "global tag 1,global tag 2,local tag") {
				AssetCategorization.viewTag(tagName = ${tagName});
			}

			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name No Tag");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntry(entry = "Test Name No Tag");
		}
	}

	@description = "This is a test for LPS-145292. The user could view new icons on dropdown menu of tags admin."
	@priority = 4
	test ViewDropdownMenuIconsInTagsAdmin {
		task ("Given a Tags creator has a new tag") {
			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "tagname");
		}

		task ("When the Tags creator opens the dropdown menu of new tag") {
			Tag.openTagsAdmin(siteURLKey = "test-site-name");

			LexiconEntry.openEntryMenu(rowEntry = "tagname");
		}

		task ("Then the Tags creator can see the new icons on the dropdown menu") {
			DropdownMenuItem.viewIcon(iconList = "pencil,merge,trash");
		}

		task ("When the Tags creator opens the dropdown menu of management toolbar after selecting new tag") {
			ManagementBar.clickSelectAllCheckbox();

			Click(locator1 = "ManagementBar#MORE_ACTIONS_ELLIPSIS");
		}

		task ("Then the Tags creator can see the new icons on the dropdown menu") {
			DropdownMenuItem.viewIcon(iconList = "merge,trash");
		}
	}

	@description = "LPS-169877 Only global tags are shown when the object scope is set to an instance"
	@priority = 5
	test ViewOnlyGlobalTagsShownInObjectInstanceScope {
		task ("Given an object with scope to an instance") {
			for (var i : list "1,2") {
				JSONAssettag.addTag(
					groupName = "Global",
					tagName = "global tag ${i}");
			}

			JSONAssettag.addTag(
				groupName = "Test Site Name",
				tagName = "local tag");

			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout Name",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Control Panel > Object");

			Button.clickSave();

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			ObjectAdmin.goToLayoutsTab();

			ObjectAdmin.selectKebabMenuOption(kebabOption = "View");

			ObjectAdmin.markLayoutAsDefault();

			ObjectAdmin.goToLayoutTabOnLayouts();

			ObjectAdmin.addTabFieldsOnLayout(
				blockName = "Block 1",
				columnsNumber = 1,
				fieldLabelName = "Name",
				tabName = "Field Tab");

			ObjectAdmin.addCategorizationOnLayoutTab();

			Button.clickSave();

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Form Components",
				fragmentName = "Form Container");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Form Container");

			Alert.viewErrorMessage(errorMessage = "Fragments cannot be placed inside an unmapped form container.");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.viewInputStyle(
				label = "Name",
				showLabel = "true",
				type = "text-input");

			PageEditor.viewFragmentNotPresent(fragmentName = "Tags");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Text Input");

			PageEditor.publish();
		}

		task ("When we add tags as input") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name");

			Click(
				key_fieldLabel = "Tags",
				key_text = "Select",
				locator1 = "Button#ANY_WITH_LABEL");
		}

		task ("Then only global tags are shown") {
			IFrame.selectTagFrame();

			AssertElementNotPresent(
				key_rowEntry = "local tag",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");

			SelectFrameTop();

			Click(locator1 = "Modal#CLOSE_BUTTON");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2");

			for (var i : list "1,2") {
				AssertTextEquals(
					key_tagName = "global tag ${i}",
					locator1 = "AssetCategorization#TAG",
					value1 = "global tag ${i}");
			}

			AssetCategorization.removeTag(tagName = "global tag 1");

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1");

			for (var i : list "1,2") {
				AssetCategorization.removeTag(tagName = "global tag ${i}");
			}

			AssetCategorization.selectTag(
				skipSelectFrame = "true",
				tagNameList = "global tag 1,global tag 2");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ApplicationsMenu.gotoPortlet(
				category = "Object",
				panel = "Control Panel",
				portlet = "MyObject");

			ObjectPortlet.viewEntryDetails(entry = "Test Name");

			for (var i : list "1,2") {
				AssetCategorization.viewTag(tagName = "global tag ${i}");
			}

			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			FormViewBuilder.editText(
				fieldName = "Name",
				fieldValue = "Test Name No Tag");

			Button.clickSubmitButton();

			AssertTextPresent(value1 = "Thank you. Your information was successfully received.");

			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "MyObject");

			ObjectPortlet.viewEntry(entry = "Test Name No Tag");
		}
	}

	@description = "View pagination in tags admin."
	@priority = 4
	test ViewTagsPagination {
		task ("Add twenty four tags") {
			var i = 1;

			while (${i} != 10) {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname0${i}");

				var i = ${i} + 1;
			}

			while (${i} != 26) {
				JSONAssettag.addTag(
					groupName = "Test Site Name",
					tagName = "tagname${i}");

				var i = ${i} + 1;
			}
		}

		task ("View pagination results for 20 entries") {
			Tag.openTagsAdmin(siteURLKey = "Test Site Name");

			AssertTextEquals.assertPartialText(
				locator1 = "Pagination#ITEMS_PER_PAGE_SELECT",
				value1 = "20 Entries");

			Pagination.viewResults(results = "Showing 1 to 20 of 24 entries.");
		}

		task ("Change the amount of items per page to 4") {
			Pagination.changePagination(itemsPerPage = 4);

			Pagination.viewResults(results = "Showing 1 to 4 of 24 entries.");
		}

		task ("Change the amount of items per page to 8") {
			Pagination.changePagination(itemsPerPage = 8);

			Pagination.viewResults(results = "Showing 1 to 8 of 24 entries.");
		}

		task ("Change the amount of items per page to 40") {
			Pagination.changePagination(itemsPerPage = 40);

			Pagination.viewResults(results = "Showing 1 to 24 of 24 entries.");
		}
	}

	@description = "LPS-169877 A warning message is shown if the object does not have categorization enabled and we try to use tags"
	@priority = 4
	test ViewWarningMessageWithCategorizationDisabled {
		task ("Given an object with categorization disabled") {
			ObjectAdmin.addObjectViaAPI(
				labelName = "MyObject",
				objectName = "MyObject",
				pluralLabelName = "MyObject",
				scope = "site");

			ObjectAdmin.addObjectFieldViaAPI(
				fieldBusinessType = "Text",
				fieldLabelName = "Name",
				fieldName = "name",
				fieldType = "String",
				isRequired = "false",
				objectName = "MyObject");

			ObjectAdmin.publishObjectViaAPI(objectName = "MyObject");

			ObjectAdmin.openObjectAdmin();

			ObjectPortlet.selectCustomObject(label = "MyObject");

			CreateObject.selectPanelCategoryKey(panelCategoryKey = "Site Administration > Categorization");

			Uncheck.uncheckToggleSwitch(locator1 = "ObjectPortlet#ENABLE_CATEGORIZATION_BUTTON");

			Button.clickSave();
		}

		task ("When we try to use tag fragments") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Form Components",
				fragmentName = "Form Container");

			PageEditor.mapFormContainerToObject(contentType = "MyObject");

			PageEditor.addFragmentToFormContainer(
				collectionName = "Form Components",
				fragmentName = "Tags",
				targetFragmentName = "Text Input");
		}

		task ("Then a warning message is shown") {
			Alert.viewInfoMessageSpecific(infoMessage = "Categorization is disabled for the selected content. To show categories in this fragment, categorization must be enabled.");
		}
	}

}