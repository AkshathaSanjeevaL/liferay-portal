@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testcase.url = "http://localhost:8080";
	property testray.component.names = "Echo Accessibility";
	property testray.main.component.name = "Web Content Administration";

	setUp {
		task ("Sign in") {
			User.firstLoginPG();
		}
	}

	@description = "This is a test for LPS-171044. The site designer could select or deselect a parent node and all its child items via keyboard shortcuts."
	@priority = 3
	test SelectOrDeselectParentNodeAndChildItemsViaKeyboard {
		task ("Given a site designer has a web content") {
			var siteName = StringUtil.randomString(8);

			var siteURLKey = StringUtil.toLowerCase(${siteName});

			HeadlessSite.addSite(siteName = ${siteName});

			JSONCategory.addVocabulary(
				groupName = ${siteName},
				title = "Vocabulary Name");

			JSONCategory.addCategory(
				groupName = ${siteName},
				title = "Category Name",
				vocabularyName = "Vocabulary Name");

			for (var i : list "1,2,3") {
				JSONCategory.addSubcategory(
					groupName = ${siteName},
					parentCategoryName = "Category Name",
					title = "Subcategory Name ${i}",
					vocabularyName = "Vocabulary Name");
			}

			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = ${siteName},
				title = "Web Content Title");
		}

		task ("When the site designer opens the Select Category modal") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = ${siteURLKey});

			WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

			Panel.expandPanel(panel = "Categorization");

			Click(
				key_fieldLabel = "Vocabulary Name",
				key_text = "Select",
				locator1 = "Button#ANY_WITH_LABEL");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Portlet.expandTree();
		}

		task ("Then the site designer could select or deselect parent node and child items via keyboard and mouse") {
			KeyPress(
				key_text = "plus",
				locator1 = "Icon#ANY",
				value1 = "\TAB");

			Type.sendKeys(
				locator1 = "//div[@role='treeitem'][normalize-space()='Category Name']",
				value1 = "keys=SHIFT,ENTER");

			for (var categoryName : list "Category Name,Subcategory Name 1,Subcategory Name 2,Subcategory Name 3") {
				AssertChecked.assertCheckedNotVisible(
					key_nodeName = ${categoryName},
					locator1 = "Treeview#NODE_CHECKBOX");
			}

			Type.sendKeys(
				locator1 = "//div[@role='treeitem'][normalize-space()='Category Name']",
				value1 = "keys=SHIFT,ENTER");

			for (var categoryName : list "Category Name,Subcategory Name 1,Subcategory Name 2,Subcategory Name 3") {
				AssertNotChecked.assertNotCheckedNotVisible(
					key_nodeName = ${categoryName},
					locator1 = "Treeview#NODE_CHECKBOX");
			}
		}
	}

}