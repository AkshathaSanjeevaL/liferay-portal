@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "feature.flag.LPS-164563=true";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			FrontendDataSetAdmin.deleteAllDatasetEntries();
		}
	}

	@description = "LPS-178777. Confirm that the user can create a dataset view via API"
	@priority = 5
	test CanCreateDatasetEntryViaAPI {
		task ("Given creating a Data Set via API") {
			FrontendDataSetAdmin.goToDatasetAdminPage();

			FrontendDataSetAdmin.createDatasetEntryViaAPI(
				datasetName = "Test Dataset",
				restApplication = "/c/fdsentries");
		}

		task ("and given access Datasets admin page") {
			FrontendDataSetAdmin.goToDatasetAdminPage();

			WaitForElementPresent(
				key_itemName = "Test Dataset",
				locator1 = "FrontendDataSetAdmin#TABLE_CELL");
		}

		task ("Then the data set is shown in the list") {
			AssertElementPresent(
				key_itemName = "Test Dataset",
				locator1 = "FrontendDataSetAdmin#TABLE_CELL");
		}
	}

	@description = "LPS-181355. Confirm that views can be created through API"
	@priority = 4
	test CanCreateViewsThroughAPI {
		task ("And Add a Dataset") {
			ApplicationsMenu.gotoPortlet(
				category = "Object",
				panel = "Control Panel",
				portlet = "Datasets");

			FrontendDataSetAdmin.createDataSet(
				key_name = "Dataset Test",
				key_type = "/c/fdsentries");
		}

		task ("And a dataset view created via api") {
			FrontendDataSetAdmin.createFDSViewViaAPI(
				datasetName = "Dataset Test",
				dataSetViewDescription = "FDSViewDescription",
				dataSetViewName = "View Test");
		}

		task ("When access to Datasets admin page") {
			ApplicationsMenu.gotoPortlet(
				category = "Object",
				panel = "Control Panel",
				portlet = "Datasets");
		}

		task ("And going to the View page of the dataset created.") {
			FrontendDataSetAdmin.goToViews(dataSetName = "Dataset Test");
		}

		task ("Then Confirm the dataset view was created.") {
			AssertElementPresent(
				entryName = "View Test",
				locator1 = "FrontendDataSetAdmin#DATA_SET_VIEW_ENTRY_NAME");
		}
	}

}