@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testcase.url = "http://localhost:8080";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Sign in") {
			User.firstLoginPG();
		}
	}

	@description = "This is a test for LPS-158527. The guest shouldn't be allowed to preview changes."
	@priority = 4
	test CannotPreviewChangesAsGuest {
		var siteName = StringUtil.randomString(8);

		task ("Given a guest has a content pages with draft changes") {
			HeadlessSite.addSite(siteName = ${siteName});

			JSONLayout.addPublicLayout(
				groupName = ${siteName},
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = ${siteName});

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			PageEditor.previewInANewTab();

			var previewURL = Navigator.getCurrentURL();

			Close.closeWindow();
		}

		task ("When the guest access to the content page at preview mode") {
			User.logoutPG();

			Navigator.openSpecificURL(url = ${previewURL});
		}

		task ("Then the guest should see not found") {
			Alert.viewErrorMessage(errorMessage = "Your request failed to complete.");
		}
	}

	@description = "This is a test for LPS-153367. The page creator could preview changes in a new tab."
	@priority = 5
	test PreviewChangesInANewTab {
		property portal.acceptance = "true";

		var siteName = StringUtil.randomString(8);

		task ("Given a page creator has a Heading fragment on a content page") {
			HeadlessSite.addSite(siteName = ${siteName});

			JSONLayout.addPublicLayout(
				groupName = ${siteName},
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = ${siteName});

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("When the page creator edits the inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "New editable fragment text");
		}

		task ("Then the page creator could preview changes in a new tab") {
			PageEditor.previewInANewTab();

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "New editable fragment text");
		}
	}

}